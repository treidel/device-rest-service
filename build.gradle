def versionOverrides = [
	"org.springframework:spring-core": "4.0.6.RELEASE",
	"org.springframework:spring-tx": "4.0.6.RELEASE",
	"org.springframework:spring-aop": "4.0.6.RELEASE",
	"org.springframework:spring-context": "4.0.6.RELEASE",
	"org.springframework:spring-webmvc": "4.0.6.RELEASE",
	"org.springframework:spring-test" : "4.0.6.RELEASE",
	"org.springframework:spring-beans": "4.0.6.RELEASE",	
	"org.springframework.data:spring-data-commons":"1.8.0.RELEASE",
	"org.springframework.hateoas:spring-hateoas":"0.16.0.RELEASE",
	"org.springframework.plugin:spring-plugin-core":"1.1.0.RELEASE",
	"org.springframework.security:spring-security-config":"3.2.4.RELEASE",
	"org.springframework.security:spring-security-web":"3.2.4.RELEASE",	
	"org.springframework.boot:spring-boot-starter-test":"1.1.4.RELEASE",
	"com.fasterxml.jackson.core:jackson-databind" : "2.4.3",
	"com.fasterxml.jackson.datatype:jackson-datatype-joda" : "2.4.3",
	"com.jayway.jsonpath:json-path":"0.9.1",	
	"com.amazonaws:aws-java-sdk" : "1.9.0",
	"org.apache.storm:storm-core" : "0.9.2-incubating",
	"org.apache.curator:curator-framework" : "2.6.0",
	"org.apache.commons:commons-lang3" : "3.3.2",
	"org.apache.httpcomponents:httpclient" : "4.3.2",
	"org.apache.httpcomponents:httpcore" : "4.3.2",
	"com.esotericsoftware.kryo:kryo" : "2.24.0",
	"org.clojars.reideltj:kinesis-storm-spout":"1.0.4-SNAPSHOT",
	"commons-beanutils:commons-beanutils":"1.9.2",
	"org.slf4j:slf4j-api":"1.7.7",
	"org.apache.james:apache-mime4j":"0.7.2"
	
]

subprojects {
	apply plugin: 'java'
	group = 'com.fancypants'
	version = '1.0'

	repositories {
		mavenCentral()
		maven { url "http://repo.spring.io/libs-release" }
		maven { url "http://clojars.org/repo" }
		maven { url "http://repository-opensourceagility.forge.cloudbees.com/snapshot" }
	}
	
	dependencies {
		compile("org.slf4j:slf4j-api")
	}

	configurations.all {
		resolutionStrategy.eachDependency { DependencyResolveDetails details ->
			def overrideVersion = versionOverrides[details.requested.group + ":" + details.requested.name]
			if (overrideVersion != null && details.requested.version != overrideVersion) {
				logger.info "Overriding dependency ${details.requested.group}:${details.requested.name} version ${details.requested.version} --> $overrideVersion"
				details.useVersion overrideVersion
			}		
		}
   	}
}

project(':data') { 
	dependencies {
		compile project(':common')
	}
}

project(':data-dynamodb') {
	dependencies {
		compile project(':common')
		compile project(':data')
	}
}

project(':rest') {
        dependencies {
                compile project(':common')
		compile project(':data')
        }
}

project(':stream') {	
	dependencies {
		compile project(':common')
	}
}

project(':stream-kinesis') {
	dependencies {
		compile project(':common')
		compile project(':stream')
		compile project(':data')
	}
}

project(':device') { 
	dependencies {
		compile project(':common')
		compile project(':data')
		compile project(':stream')
	}
}

project(':rest-device') {
	dependencies {
		compile project(':common')
		compile project(':rest')
		compile project(':device')
		compile project(':data-dynamodb')
		compile project(':stream-kinesis')
	}
}

project(':app') {
	dependencies {
		compile project(':common')
		compile project(':data')
	}
}

project(':rest-app') {
	dependencies {
		compile project(':common')
		compile project(':rest')
		compile project(':app')
		compile project(':device')
	}
}


project(':processing') { 
	dependencies { 
		compile project(':common')
		compile project(':data')
		compile project(':stream')
	}
}
